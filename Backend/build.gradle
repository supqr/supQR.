plugins {
    id 'java'
    id 'application'
    id "nu.studer.jooq" version "3.0.3"
}

group = "com.coderbunker"
version = "1.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: "io.dropwizard", name: "dropwizard-core", version: "1.3.9"
    implementation group: "com.bendb.dropwizard", name: "dropwizard-jooq", version: "1.2.0-0"
    implementation group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "2.4.1"
    compileOnly "org.projectlombok:lombok:1.18.6"
    annotationProcessor "org.projectlombok:lombok:1.18.6"


    testImplementation group: "junit", name: "junit", version: "4.12"

    jooqRuntime group: 'com.h2database', name: 'h2', version: '1.4.196'
}

jooq {
    version = "3.11.11"
    edition = "OSS"
    supqr(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:mem:;MODE=MYSQL;INIT=RUNSCRIPT FROM \'src/main/resources/supqr.ddl.sql\''
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'supqr'
                includes = '.*'
                excludes = ''
            }
            generate {
                relations = true
                deprecated = false
                daos = true
                records = true
                pojos = true
                pojosEqualsAndHashCode = true
                fluentSetters = true
            }

        }
    }

}