plugins {
    id 'java'
    id 'application'
    id "nu.studer.jooq" version "3.0.3"
    id "org.flywaydb.flyway" version "5.2.4"
}

group = "com.coderbunker"
version = "1.0-SNAPSHOT"

def db_driver = 'org.h2.Driver'
def db_url = 'jdbc:h2:file:./build/db/jooq-db;MODE=MYSQL'
def db_user = 'sa'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    flywayMigration
}

dependencies {
    implementation group: "io.dropwizard", name: "dropwizard-core", version: "1.3.9"
    implementation group: "com.bendb.dropwizard", name: "dropwizard-jooq", version: "1.2.0-0"
    implementation group: "org.mariadb.jdbc", name: "mariadb-java-client", version: "2.4.1"
    implementation group: "commons-io", name: "commons-io", version: "2.6"
    implementation "org.flywaydb:flyway-core:5.2.4"
    compileOnly "org.projectlombok:lombok:1.18.6"
    annotationProcessor "org.projectlombok:lombok:1.18.6"


    testImplementation group: "junit", name: "junit", version: "4.12"

    jooqRuntime group: 'com.h2database', name: 'h2', version: '1.4.196'
    flywayMigration 'com.h2database:h2:1.4.196'
}

jooq {
    version = "3.11.11"
    edition = "OSS"
    supqr(sourceSets.main) {
        jdbc {
            driver = db_driver
            url = db_url
            user = db_user
            password = ''
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'public'
                includes = '.*'
                excludes = ''
                outputSchemaToDefault = true
            }
            generate {
                relations = true
                deprecated = false
                daos = true
                records = true
                pojos = true
                pojosEqualsAndHashCode = true
                fluentSetters = true
            }

        }
    }

}

flyway {
    driver = db_driver
    url = db_url
    user = db_user
    password = ''
    configurations = ['compile', 'flywayMigration']
}

generateSupqrJooqSchemaSource.dependsOn flywayMigrate